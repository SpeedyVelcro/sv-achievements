class_name CollectionAchievementObjective
extends AchievementObjective
## Collection of string-keyed objectives.
##
## An [AchievementObjective] that collates a collection of AchievementObjectives
## and completes when all of those objectives are complete. This is almost
## identical in behaviour to [IndexedAchievementObjective], however the collection
## is variant-keyed (although you will probably want to just use strings), allowing
## more human-friendly access to the underlying objectives.
##
## This might be useful, for example, for an achievement to complete multiple
## distinct areas in an open-world game, where they can be completed in any order.
## If you have a red area, a blue area, and a yellow area, you might key the
## objective collection with a String. And then to complete an objective, you
## could call e.g. [Achievement].objective.collection["red"].complete()

## Variant-keyed collection of objectives. Assign this at creation. Do not add
## or remove objectives at runtime as this will mess up the internal signalling.
## It is recommended you key this with meaningful strings for ease-of-use.
@export var collection: Dictionary[Variant, AchievementObjective]:
	set(value):
		_disconnect_children()
		collection = value
		_connect_children()
	get:
		return collection


## Returns the number of objectives in the collection
func count() -> int:
	if collection == null:
		return 0
	
	return collection.size()


## Returns the number of completed objectives in the collection
func count_complete() -> int:
	if collection == null:
		return 0
	
	return collection.values().filter(func (o: AchievementObjective) -> bool: return o.completion_state).size()


func _connect_children() -> void:
	for objective: AchievementObjective in collection.values():
		objective.completed.connect(_on_child_objective_completed)


func _disconnect_children() -> void:
	if collection == null:
		return
	
	for objective: AchievementObjective in collection.values():
		if objective.completed.is_connected(_on_child_objective_completed):
			objective.completed.connect(_on_child_objective_completed)


# Signal connection
func _on_child_objective_completed() -> void:
	if collection.values().all(func (o: AchievementObjective) -> bool: return o.completion_state):
		complete()
